buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.netflix.rxjava:rxjava-core:0.19.1'
    compile 'com.netflix.rxjava:rxjava-android:0.19.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.+'
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.1'
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release {
            try {
                storeFile file("example.keystore")
                storePassword KEYSTORE_PASSWORD
                keyAlias "client"
                keyPassword KEY_PASSWORD
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        debug {
            runProguard false
        }
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

// get location of adb
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def adb = properties.getProperty('sdk.dir') + "/platform-tools/adb"

task checkstyle(type: Checkstyle) {
    description 'Checks if the code is somewhat acceptable'
    configFile file('./spaghetti-checkstyle.xml')
    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files(project.configurations.compile.asPath)
}

task runAppRelease(dependsOn: 'assembleRelease') {
    description 'Runs the release-flavour of the app on the connected device'
    doLast {
        exec{commandLine "$adb", "uninstall", "com.futurice.project"}
        exec{commandLine "$adb", "install", "build/outputs/apk/app-release.apk"}
        exec{commandLine "$adb", "shell", "am", "start", "-n", "com.futurice.project/.MainActivity"}
    }
}
